plugins {
    id 'java'
    id 'antlr'
}

group = 'io.github.IndoorSpatial'
version = '0.1.0'
description = 'CQL (Common Query Language) parser generated by ANTLR4 (cql2g4)'

repositories {
    mavenCentral()
}

configurations {
    antlrTool
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    implementation "com.fasterxml.jackson.core:jackson-core:2.19.1"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.19.1"

    implementation 'org.locationtech.jts:jts-core:1.19.0'
    implementation 'org.locationtech.jts.io:jts-io-common:1.19.0'

    antlr "org.antlr:antlr4:4.13.2"
    antlrTool "org.antlr:antlr4:4.13.2"

    implementation 'com.github.jsqlparser:jsqlparser:4.6'
}

tasks.register('generateCppParser', JavaExec) {
    group = "build"
    description = "Generate C++ parser from ANTLR4 grammars"
    mainClass = "org.antlr.v4.Tool"
    classpath = configurations.antlrTool
    args = [
            "-o", "build/generated/ANTLR4/Cpp",
            "-Dlanguage=Cpp",
            "-listener",
            "-visitor",
            "-package", "cql2g4cpp",
            "src/main/resources/antlr4/Cql2Parser.g4",
            "src/main/resources/antlr4/Cql2Lexer.g4"
    ]
}

tasks.register('generateJavaParser', JavaExec) {
    group = "build"
    description = "Generate Java parser from ANTLR4 grammars"
    mainClass = "org.antlr.v4.Tool"
    classpath = configurations.antlrTool
    args = [
            "-o", "build/generated/ANTLR4/Java/io/github/IndoorSpatial/cql2",
            "-Dlanguage=Java",
            "-listener",
            "-visitor",
            "-package", "io.github.IndoorSpatial.cql2",
            "src/main/resources/antlr4/Cql2Parser.g4",
            "src/main/resources/antlr4/Cql2Lexer.g4"
    ]
}

tasks.named('compileJava') {
    dependsOn 'generateJavaParser', 'generateCppParser'
}

sourceSets {
    main {
        java {
            srcDir 'build/generated/ANTLR4/Java'
        }
    }
}

test {
    useJUnitPlatform()
}